apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'io.fabric'

def versionMajor = 0
def versionMinor = 0
def versionPatch = 0
def versionBuild = 1

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.example.films"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode getVersionCode(versionMajor, versionMinor, versionPatch, versionBuild)
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        applicationVariants.all { variant ->
            renameAPK(variant, defaultConfig)
        }
    }
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "server"
    productFlavors {
        mock {
            dimension "server"
            applicationIdSuffix ".mock"
            versionNameSuffix "-mock"
            buildConfigField("String", "BASE_URL", "\"${base_url_mock}\"")
        }
        staging {
            dimension "server"
            applicationIdSuffix ".staging"
            versionNameSuffix "-staging"
            buildConfigField("String", "BASE_URL", "\"${base_url_staging}\"")
        }
        prod {
            dimension "server"
            applicationIdSuffix ".prod"
            versionNameSuffix "-prod"
            buildConfigField("String", "BASE_URL", "\"${base_url_prod}\"")
        }
    }
    sourceSets {
        staging.java.srcDir 'src/common/java'
        prod.java.srcDir 'src/common/java'
    }
    variantFilter { variant ->
        def names = variant.flavors*.name
        if (names.contains("mock") && variant.buildType.name == "release") {
            setIgnore(true)
        }
    }
    compileOptions {
        targetCompatibility = "8"
        sourceCompatibility = "8"
    }
}

static def getVersionCode(versionMajor, versionMinor, versionPatch, versionBuild) {
    return versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionBuild
}

static def renameAPK(variant, defaultConfig) {
    variant.outputs.all { output ->
        outputFileName = "films_v" + defaultConfig.versionName + "(" + defaultConfig.versionCode + ").apk"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //AndroidX
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    kapt 'androidx.lifecycle:lifecycle-compiler:2.0.0'

    //Firebase
    implementation 'com.google.firebase:firebase-core:17.1.0'
    implementation 'com.google.firebase:firebase-ads:18.1.1'
    implementation 'com.google.firebase:firebase-analytics:17.1.0'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    //Dependency Injection
    implementation 'org.koin:koin-android:2.0.1'
    implementation 'org.koin:koin-androidx-scope:2.0.1'
    implementation 'org.koin:koin-androidx-viewmodel:2.0.1'

    //Networking
    implementation 'com.squareup.okhttp3:okhttp:3.14.2'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.14.2'
    implementation 'com.squareup.retrofit2:retrofit:2.6.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.0'
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    implementation 'com.github.bumptech.glide:okhttp-integration:4.9.0'
    kapt 'com.github.bumptech.glide:compiler:4.9.0'

    //ReactiveX
    implementation 'io.reactivex.rxjava2:rxkotlin:2.3.0'
    implementation "io.reactivex.rxjava2:rxjava:2.2.9"
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    //Logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    //Testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

apply plugin: 'com.google.gms.google-services'
